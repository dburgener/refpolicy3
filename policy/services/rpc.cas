// SPDX-License-Identifier: GPL-2.0

let nfs_export_all_ro = false;
/// Export all non-security files with read-only access.

let nfs_export_all_rw = false;
/// Export all non-security files with read-write access.

//
// Extends
//
extend kernel_t {
    fs_t.getattr_superblock();

    gssproxy_t.unix_stream_connect();

    // nfs kernel server needs kernel UDP access. It is less risky and painful
    // to just give it everything.
    network.udp_server();

    nfsd_t.rw_inherited_tcp_sockets();
    nfsd_t.rw_inherited_udp_sockets();
    nfsd_t.state.list(); var_lib_t.list();

    shadow_t.dontaudit_getattr();

    public_content_t.read();

    public_content_rw_t.manage();

    if (nfs_export_all_ro) {
        noxattr_filesystem.getattr_superblock();
        noxattr_filesystem<common_file>.read();

        file_type<common_file>.read(); drop security_file<file_type>.read();
    }

    if (nfs_export_all_rw) {
        noxattr_filesystem.getattr_superblock();
        noxattr_filesystem<common_file>.read();

        file_type<common_file>.manage(); drop security_file<file_type>.manage();
    }
}

extend mount_t {
    network.use_portmap();

    rpc_pipefs_t.list();

    rpcd_t.domtrans();
}

extend systemd_t {
    // var-lib-nfs-rpc_pipefs.mount creates /var/lib/nfs/rpc_pipefs
    // if it does not exist
    nfsd_t.state.manage(); var_lib_t.list(); var_t.list();

    nfsd_fs_t.rw();

    rpc_pipefs_t.rw();
}
